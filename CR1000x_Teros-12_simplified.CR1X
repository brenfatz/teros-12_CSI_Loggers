'Brendan Fatzinger (brendan.fatzinger@usu.edu) and Bruce Bugbee; 2022
'Teros 12 sensors/crop steering/precision drought stress

'Simplified code for measuring one Teros 12 sensor in rockwool.
'The current calibration is also reasonably accurate in soilless media.
'Additional Meter Group calibrations are listed below in the supplemental materials.
'Custom calibrations can be implemented for other SDI12 soils sensors, 
'but retaining the stock calibrations for Teros 12 specific applications (provided) is recommended.
'________________________________________________________________________________________________________________

'Declare variables, constants, and arrays

'Raw sensor data saved as a three component array in the following order:
'(1)= raw sensor output
'(2)= temperature
'(3)= electrical conductivity
Public Sensor_1_Out(3)
Public Q 'local water content (VWC)
Public ECw25 '(dS/m) estimated pore water EC at 25C
Public dx 'intermediary normalization factor for raw sensor signal
Public de_dx_14011993 'derivative of intermediary
Public dt 'normalization factor for raw sensor signal
Public x20 'normalizes raw signal to what it would be @ 20 C
Public SR 'calibration factor to account for EC dependency on VWC
Const de_dT = -0.3744 'dPermittivity/DTemperature for water
Const xC = 3300 'determining value of x for Q check and calculation
Const Qmax = .97 'maximum allowable calculated VWC (not accurate above this value for the RW calibration)
'calibration constants derived from hanging water column experiments performed by Meter Group:
Const m1 = -0.8615
Const m2 = 5.3077e-04
Const aH = -0.08
Const bH = -6.6346e-3
Const uL = 0.08
Const SR_min_14011993 = 3.7e-3
Const uA = 0.825
Const r1 = -1329
Const r2 = 1397
Const r3 = -199.9
Const r4 = 10.47
Const s1 = -636.5
Const s2 = -313
Const s3 = 769.5
Const k2 = -7.00655
Const k1 = 14.85751
Const k0 = -6.85096

'Assign new variables to outputs from sensor array
Alias Sensor_1_Out(1) = x 'raw signal from sensor
Alias Sensor_1_Out(2) = T 'Temperature (C)
Alias Sensor_1_Out(3) = EC25 '(dS/m) bulk EC at 25C

DataTable (Data_Table,1,-1)
  DataInterval (0,10,Sec,10) 'change data save interval here
  Sample (1,Q,IEEE4) 'save VWC to data table
  Sample (1,ECw25,IEEE4) 'save EC to data table
  Sample (1,x,FP2) 'save raw sensor signal to data table
  Sample (1,T,FP2) 'save temperature to data table
  Sample (1,de_dx_14011993,FP2) 'save permativitty
  Sample (1,dt,FP2) 'save normalization factor for temperature
  Sample (1,dx,FP2) 'save normalization factor for raw sensor signal
EndTable

BeginProg
  Scan (5,Sec,0,0) 'change scan interval here
    'Ask the sensors to send their data to the array declared above via the the following command:
    SDI12Recorder (Sensor_1_Out,C1,1,"M!",1.0,0)
    '||||||||||||||||||||||| Calibration formula/coefficients are deployed here |||||||||||||||||||||||
    'Meter Group calibration for rockwool:
    'convert x at temperature T into x20, at 20C
    de_dx_14011993 = 2*(2.887e-9*x^3-2.08e-5*x^2+5.276e-2*x-43.39)*(3*2.887e-9*x^2-2*2.08e-5*x+5.276e-2)
    dt = 20-T 'normalize temperature to 20C
    dx = de_dT/de_dx_14011993*dt 'change in x with respect to permittivity and temperature
    x20 = x+dx 'normalize raw sensor signal at 20C
    'estimate volumetric water content
    If x20 < xC 'if normalized raw signal is less than 3300
      Q = m2*x20+m1 'use this calculated value for VWC
    Else 'if greater than 3300
      Q = aH*EXP(bH*(x20-xC))+Qmax 'use this calculated value for VWC
    EndIf 'close "if" statement
    'estimate pore water EC and calculate specific calibration factors
    If Q < uL 'if VWC is less than 0.08
      SR = SR_min_14011993 'use this EC calibration factor
    ElseIf Q >= uL AND Q < uA 'secondary parameter check, if VWC is between 0.08 and 0.825
      SR = (r1*Q^3+r2*Q^2+r3*Q+r4)/(Q^3+s1*Q^2+s2*Q+s3) 'use this EC calibration factor
    ElseIf Q >= uA 'Tertiary parameter check, if VWC is greater than or equal to 0.825
      SR = k2*Q^2+k1*Q+k0 'use this EC calibration factor
    EndIf 'close "if" statement
    ECw25 = EC25/SR 'divide by calibration factor to account for EC dependency on VWC

'|||||||||||||||||||||||||||||||  Supplemental calibrations  ||||||||||||||||||||||||||||||| 
'- Comment out all the the RW calibration above...
'- Then uncomment one of the below calibration equations you wish to use:
    'Meter Group calibration for soilless media:
    'Q = 3.879E - 4*x - 0.6956 'uncomment this line
    'Meter Group calibration for mineral soils:
    'Q = 6.771E - 10*x^3 - 5.105E-6*x^2 + 1.302E-2*x - 10.848 'uncomment this line
    
    CallTable Data_Table 'tell datalogger to compile and save data to the data table. 
  NextScan 'check for another scan declaration
EndProg 

